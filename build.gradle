plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
	id 'jvm-test-suite'
	id 'com.gradleup.shadow' version '8.3.0'
}

group = 'net.enderturret'
version = '1.4.0'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
java.withSourcesJar()

repositories {
    mavenCentral()
}

sourceSets {
    patcher {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    patcherTest {
        compileClasspath += sourceSets.patcher.output
        runtimeClasspath += sourceSets.patcher.output
    }
}

configurations {
    patcherImplementation.extendsFrom implementation
    patcherCompileOnly.extendsFrom compileOnly
    testCompileOnly.extendsFrom compileOnly
}

testing {
	suites {
		test {
		    useJUnitJupiter()
		}
	}
}

dependencies {
    api 'com.google.code.gson:gson:2.9.0'
    compileOnly 'org.jetbrains:annotations:24.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
}

task patcherJar(type: Jar, dependsOn: compilePatcherJava) {
    archiveClassifier = 'patcher'
    from (sourceSets.patcher.output) {
        exclude 'net/enderturret/patched/Patched.class'
        exclude 'net/enderturret/patched/Patched$Settings.class'
    }
}

task patcherSourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'patcher-sources'
    from (sourceSets.patcher.allSource) {
        exclude 'net/enderturret/patched/Patched.java'
    }
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveClassifier = 'cli'
    from sourceSets.patcher.output
}

artifacts {
    archives patcherJar, patcherSourcesJar, shadowJar
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Patched'
                description = 'A json patching library that implements RFC 6902 with some optional extensions.'
                url = 'https://github.com/EnderTurret/Patched'
                licenses {
                    license {
                        name = 'LGPL-2.1-or-later'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1-standalone.html'
                        distribution = 'repo'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/EnderTurret/Patched/issues'
                }
                scm {
                    url = 'https://github.com/EnderTurret/Patched'
                    connection = 'scm:git:https://github.com/EnderTurret/Patched.git'
                    developerConnection = 'scm:git:https://github.com/EnderTurret/Patched.git'
                }
            }
            artifact patcherJar
            artifact patcherSourcesJar
        }
    }
}