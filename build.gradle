plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'net.enderturret'
version = '1.2.1'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()
}

sourceSets {
    patcher {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    patcherTest {
        compileClasspath += sourceSets.patcher.output
        runtimeClasspath += sourceSets.patcher.output
    }
}

configurations {
    patcherImplementation.extendsFrom implementation
}

dependencies {
    api 'com.google.code.gson:gson:2.9.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task patcherJar(type: Jar, dependsOn: compilePatcherJava) {
    classifier = 'patcher'
    from (sourceSets.patcher.output) {
        exclude 'net/enderturret/patched/Patched.class'
        exclude 'net/enderturret/patched/Patched$Settings.class'
    }
}

task patcherSourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'patcher-sources'
    from (sourceSets.patcher.allSource) {
        exclude 'net/enderturret/patched/Patched.java'
    }
}

shadowJar {
    classifier = 'cli'
    from sourceSets.patcher.output
}

artifacts {
    archives sourcesJar, patcherJar, patcherSourcesJar, shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Patched'
                description = 'Enables patching of Json files for mods or other uses.'
            }
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact patcherJar {
                classifier 'patcher'
            }
            artifact patcherSourcesJar {
                classifier 'patcher-sources'
            }
        }
    }
}